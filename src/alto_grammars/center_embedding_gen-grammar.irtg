{## grammar that only generates center-embedding sentences in subject position
 - the dress [ that the student [ that the man knew]  sold ] burned.
 - The cat [that the dog [that the man hit] chased] meowed.
 ##}

S! -> r{{ cnt.next() }}(NP_inanimate_nsubj, VP_inanim_subj) [0.4]
[english] *(?1, ?2)
[semantics] pre_theme(?2, ?1)

S! -> S_main_agent_E{{ cnt.next() }}(NP_animate_nsubj, VP_anim_subj) [0.45]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

S! -> r{{ cnt.next() }}(NP_animate_nsubj, VP_anim_subj_theme) [0.05]
[english] *(?1, ?2)
[semantics] theme(?2, ?1)

S! -> r{{ cnt.next() }}(NP_animate_nsubj, VP_anim_subj_recipient) [0.1]
[english] *(?1, ?2)
[semantics] pre_recipient(?2, ?1)


NP_inanimate_nsubj -> r{{ cnt.next() }}(Det, N_common_inanimate_nsubj) [0.1]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_inanimate_nsubj -> S_embedded_E{{ cnt.next() }}(Det, N_common_inanimate_nsubj, Rel_pron, VP_embedded_RC) [0.9]
[english] *(*(*(?1, ?2), ?3),?4)
[semantics] 'nmod<that>'(pre_det(?2,?1), theme(?4,?3))


NP_animate_nsubj -> r{{ cnt.next() }}(Det, N_common_animate_nsubj) [0.05]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_nsubj -> r{{ cnt.next() }}(N_prop_nsubj) [0.05]
[english] ?1
[semantics] ?1

NP_animate_nsubj -> S_embedded_E{{ cnt.next() }}(Det, N_common_animate_nsubj, Rel_pron, VP_embedded_RC) [0.9]
[english] *(*(*(?1, ?2), ?3),?4)
[semantics] 'nmod<that>'(pre_det(?2,?1), theme(?4,?3))

VP_embedded_RC -> r{{ cnt.next() }}(NP_animate_nsubj, V_unacc) [0.33]
[english] *(?1, ?2)
[semantics] agent(?2, ?1)

VP_embedded_RC -> r{{ cnt.next() }}(NP_animate_nsubj, V_trans_omissible) [0.33]
[english] *(?1, ?2)
[semantics] agent(?2, ?1)

VP_embedded_RC -> r{{ cnt.next() }}(NP_animate_nsubj, V_trans_not_omissible) [0.34]
[english] *(?1, ?2)
[semantics] agent(?2, ?1)


VP_anim_subj -> r{{ cnt.next() }}(V_unerg) [0.125]
[english] ?1
[semantics] ?1

VP_anim_subj -> r{{ cnt.next() }}(V_unacc, NP_dobj) [0.125]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_anim_subj -> r{{ cnt.next() }}(V_trans_omissible) [0.125]
[english] ?1
[semantics] ?1

VP_anim_subj -> r{{ cnt.next() }}(V_trans_omissible, NP_dobj) [0.125]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_anim_subj -> r{{ cnt.next() }}(V_trans_not_omissible, NP_dobj) [0.125]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_anim_subj -> I_main_control_V_F{{ cnt.next() }}(V_inf_taking, INF, V_inf)[0.125]
[english] *(?1, *(?2, ?3))
[semantics] xcomp(?1, ?3)

VP_anim_subj -> r{{ cnt.next() }}(V_dat, NP_inanimate_dobj, PP_iobj) [0.125]
[english] *(*(?1, ?2), ?3)
[semantics] recipient(theme(?1, ?2), ?3)

VP_anim_subj -> r{{ cnt.next() }}(V_dat, NP_animate_iobj, NP_inanimate_dobj) [0.125]
[english] *(*(?1, ?2), ?3)
[semantics] theme(recipient(?1, ?2), ?3)


{# inanimate VP-main #}
VP_inanim_subj -> S_main_unacc_theme_E{{ cnt.next() }}(V_unacc) [0.111]
[english] ?1
[semantics] ?1

{# passives #}
VP_inanim_subj -> S_main_pass_theme_E{{ cnt.next() }}(AUX, V_trans_not_omissible_pp) [0.111]
[english] *(?1, ?2)
[semantics] ?2

VP_inanim_subj -> S_main_pass_theme_E{{ cnt.next() }}(AUX, V_trans_not_omissible_pp, BY, NP_animate_nsubj_no_modifier) [0.111]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_inanim_subj -> S_main_pass_theme_E{{ cnt.next() }}(AUX, V_trans_omissible_pp) [0.111]
[english] *(?1, ?2)
[semantics] ?2

VP_inanim_subj -> S_main_pass_theme_E{{ cnt.next() }}(AUX, V_trans_omissible_pp, BY, NP_animate_nsubj_no_modifier) [0.111]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_inanim_subj -> S_main_pass_theme_E{{ cnt.next() }}(AUX,V_unacc_pp) [0.111]
[english] *(?1, ?2)
[semantics] ?2

VP_inanim_subj -> S_main_pass_theme_E{{ cnt.next() }}(AUX, V_unacc_pp, BY, NP_animate_nsubj_no_modifier) [0.111]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_inanim_subj -> S_main_pass_theme_E{{ cnt.next() }}(AUX, V_dat_pp, PP_iobj) [0.112]
[english] *(*(?1, ?2),?3)
[semantics] recipient(?2,?3)

VP_inanim_subj -> S_main_pass_theme_E{{ cnt.next() }}(AUX, V_dat_pp, PP_iobj, BY, NP_animate_nsubj_no_modifier) [0.111]
[english] *(*(*(?1, ?2),?3),*(?4,?5))
[semantics] agent(recipient(?2,?3),?5)

VP_anim_subj_recipient -> S_main_pass_recipient_E{{ cnt.next() }}(AUX, V_dat_pp, NP_inanimate_dobj) [0.5]
[english] *(*(?1, ?2),?3)
[semantics] theme(?2,?3)

VP_anim_subj_recipient -> S_main_pass_recipient_E{{ cnt.next() }}(AUX, V_dat_pp, NP_inanimate_dobj, BY, NP_animate_nsubj_no_modifier) [0.5]
[english] *(*(*(?1, ?2),?3),*(?4,?5))
[semantics] agent(theme(?2,?3),?5)

VP_anim_subj_theme -> S_main_unacc_theme_E{{ cnt.next() }}(V_unacc) [1]
[english] ?1
[semantics] ?1



NP_animate_nsubj_no_modifier -> r{{ cnt.next() }}(N_prop_nsubj) [0.5]
[english] ?1
[semantics] ?1

NP_animate_nsubj_no_modifier -> r{{ cnt.next() }}(Det, N_common_animate_nsubj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_dobj -> r{{ cnt.next() }}(NP_inanimate_dobj) [0.5]
[english] ?1
[semantics] ?1

NP_dobj -> r{{ cnt.next() }}(NP_animate_dobj) [0.5]
[english] ?1
[semantics] ?1

NP_animate_dobj -> r{{ cnt.next() }}(Det, N_common_animate_dobj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_dobj -> r{{ cnt.next() }}(N_prop_dobj) [0.5]
[english] ?1
[semantics] ?1

NP_inanimate_dobj -> r{{ cnt.next() }}(Det, N_common_inanimate_dobj) [1]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

PP_iobj -> r{{ cnt.next() }} (P_iobj,NP_animate_iobj)[1]
[english] *(?1, ?2)
[semantics] pre_case(?2, ?1)

NP_animate_iobj -> r{{ cnt.next() }}(Det, N_common_animate_iobj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_iobj -> r{{ cnt.next() }}(N_prop_iobj) [0.5]
[english] ?1
[semantics] ?1
{## Subsets of main grammar that only generate
RC clauses that modify NP in recipient position
object extracted RCs:
    - main recipient --> RC theme # the boy gave a cake to the cat that the girl saw  [0.5]
subject extracted RCs:
    - main recipient --> RC agent # the boy gave a cake to the girl that found the cat [0.4]
    - main recipient --> RC theme # the boy gave a cake to the cat that rolled [0.05]
recipient extracted RCs:
    - main recipient -->  RC recipient # the boy gave a cake to the cat that was offered a donut [0.05]
##}

S! -> S_recipient_modif_main_dat_recipient_E{{ cnt.next() }}(NP_animate_nsubj_main, V_dat, NP_animate_iobj_RC_modified, NP_inanimate_dobj) [0.45]
[english] *(*(*(?1, ?2), ?3), ?4)
[semantics] theme(recipient(agent(?2, ?1),?3),?4)

S! -> S_recipient_modif_main_dat_recipient_E{{ cnt.next() }}(NP_animate_nsubj_main, V_dat, NP_inanimate_dobj, PP_iobj_RC_modified) [0.45]
[english] *(*(*(?1, ?2), ?3), ?4)
[semantics] recipient(theme(agent(?2, ?1),?3),?4)

S! -> r{{ cnt.next() }}(NP_animate_nsubj, VP_cp) [0.1]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

VP_cp -> I_embedded_cp_V_F{{ cnt.next() }}(V_cp_taking, C, S!) [1]
[english] *(?1, *(?2, ?3))
[semantics] ccomp(?1, ?3)

PP_iobj_RC_modified -> r{{ cnt.next() }} (P_iobj, NP_animate_iobj_RC_modified)[1]
[english] *(?1, ?2)
[semantics] pre_case(?2, ?1)

NP_animate_nsubj_main -> r{{ cnt.next() }}(Det, N_common_animate_nsubj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_nsubj_main -> r{{ cnt.next() }}(N_prop_nsubj) [0.5]
[english] ?1
[semantics] ?1

{# indirect object extracted RCs #}
NP_animate_iobj_RC_modified -> S_RC_recipient_iobj_extracted_E{{ cnt.next() }}(NP_animate_iobj_RC, Rel_pron, VP_RC_pass_recipient) [0.05]
[english] *(*(?1, ?2),?3)
[semantics] 'nmod<that>'(?1, recipient(?3,?2))

VP_RC_pass_recipient -> r{{ cnt.next() }}(AUX, V_dat_pp, NP_inanimate_dobj) [0.5]
[english] *(*(?1, ?2),?3)
[semantics] theme(?2,?3)

VP_RC_pass_recipient -> r{{ cnt.next() }}(AUX, V_dat_pp, NP_inanimate_dobj, BY, NP_animate_nsubj) [0.5]
[english] *(*(*(?1, ?2),?3),*(?4,?5))
[semantics] agent(theme(?2,?3),?5)

{# object extracted RCs #}
NP_animate_iobj_RC_modified -> S_RC_theme_dobj_extracted_E{{ cnt.next() }}(NP_animate_iobj_RC, Rel_pron, VP_RC_theme) [0.5]
[english] *(*(?1, ?2),?3)
[semantics] 'nmod<that>'(?1, theme(?3,?2))

NP_animate_iobj_RC -> r{{ cnt.next() }}(Det, N_common_animate_iobj) [1]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

VP_RC_theme -> r{{ cnt.next() }}(NP_animate_nsubj, V_unacc) [0.33]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

VP_RC_theme -> r{{ cnt.next() }}(NP_animate_nsubj, V_trans_omissible) [0.33]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

VP_RC_theme -> r{{ cnt.next() }}(NP_animate_nsubj, V_trans_not_omissible) [0.34]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

{# subject extracted RCs #}
NP_animate_iobj_RC_modified -> S_RC_agent_subj_extracted_E{{ cnt.next() }}(NP_animate_iobj_RC, Rel_pron, VP_RC_agent) [0.4]
[english] *(*(?1, ?2),?3)
[semantics] 'nmod<that>'(?1, agent(?3,?2))

NP_animate_iobj_RC_modified -> S_RC_unacc_theme_subj_extracted_E{{ cnt.next() }}(NP_animate_iobj_RC, Rel_pron, VP_RC_unacc_theme) [0.05]
[english] *(*(?1, ?2),?3)
[semantics] 'nmod<that>'(?1, theme(?3,?2))

VP_RC_unacc_theme -> r{{ cnt.next() }}(V_unacc) [1]
[english] ?1
[semantics] ?1

VP_RC_agent -> r{{ cnt.next() }}(V_unerg) [0.10525]
[english] ?1
[semantics] ?1

VP_RC_agent -> r{{ cnt.next() }}(V_unacc, NP_dobj) [0.10525]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_RC_agent -> r{{ cnt.next() }}(V_trans_omissible) [0.10525]
[english] ?1
[semantics] ?1

VP_RC_agent -> r{{ cnt.next() }}(V_trans_omissible, NP_dobj) [0.10525]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_RC_agent -> r{{ cnt.next() }}(V_trans_not_omissible, NP_dobj) [0.10525]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_RC_agent -> I_RC_control_V_F{{ cnt.next() }}(V_inf_taking, INF, V_inf)[0.10525]
[english] *(?1, *(?2, ?3))
[semantics] xcomp(?1, ?3)

VP_RC_agent -> r{{ cnt.next() }}(V_dat, NP_inanimate_dobj, PP_iobj) [0.10525]
[english] *(*(?1, ?2), ?3)
[semantics] recipient(theme(?1, ?2), ?3)

VP_RC_agent -> r{{ cnt.next() }}(V_dat, NP_animate_iobj, NP_inanimate_dobj) [0.10525]
[english] *(*(?1, ?2), ?3)
[semantics] theme(recipient(?1, ?2), ?3)

{# allows to generate CP instances in the case of RC modifying the indirect object:
 - restrictions: one RC modifier per sentence:
 (don't allow: e.g.  Emma gave a cake to the boy that said that the guy that cleaned smiled)
 - CP recursion allowed: Emma gave a cake to the boy that said that the girl believed that Emma smiled.
  #}
VP_RC_agent -> I_RC_cp_V_F{{ cnt.next() }}(V_cp_taking, C, S_internal) [0.158]
[english] *(?1, *(?2, ?3))
[semantics] ccomp(?1, ?3)

NP_animate_nsubj -> r{{ cnt.next() }}(N_prop_nsubj) [0.5]
[english] ?1
[semantics] ?1

NP_animate_nsubj -> r{{ cnt.next() }}(Det, N_common_animate_nsubj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_iobj -> r{{ cnt.next() }}(Det, N_common_animate_iobj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_iobj -> r{{ cnt.next() }}(N_prop_iobj) [0.5]
[english] ?1
[semantics] ?1

NP_inanimate_dobj -> r{{ cnt.next() }}(Det, N_common_inanimate_dobj) [1]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

S_internal -> r{{ cnt.next() }}(NP_animate_nsubj, VP_external) [0.49]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

S_internal -> r{{ cnt.next() }}(VP_internal) [0.06]
[english] ?1
[semantics] ?1

S_internal -> r{{ cnt.next() }}(NP_inanimate_nsubjpass, VP_passive) [0.375]
[english] *(?1, ?2)
[semantics] pre_theme(?2, ?1)

S_internal -> r{{ cnt.next() }}(NP_animate_nsubjpass, VP_passive_dat) [0.075]
[english] *(?1, ?2)
[semantics] pre_recipient(?2, ?1)

VP_external -> r{{ cnt.next() }}(V_cp_taking, C, S_internal) [0.158]
[english] *(?1, *(?2, ?3))
[semantics] ccomp(?1, ?3)

VP_external -> r{{ cnt.next() }}(V_unerg) [0.10525]
[english] ?1
[semantics] ?1

VP_external -> r{{ cnt.next() }}(V_unacc, NP_dobj) [0.10525]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_external -> r{{ cnt.next() }}(V_trans_omissible) [0.10525]
[english] ?1
[semantics] ?1

VP_external -> r{{ cnt.next() }}(V_trans_omissible, NP_dobj) [0.10525]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_external -> r{{ cnt.next() }}(V_trans_not_omissible, NP_dobj) [0.10525]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_external -> r{{ cnt.next() }}(V_inf_taking, INF, V_inf)[0.10525]
[english] *(?1, *(?2, ?3))
[semantics] xcomp(?1, ?3)

VP_external -> r{{ cnt.next() }}(V_dat, NP_inanimate_dobj, PP_iobj) [0.10525]
[english] *(*(?1, ?2), ?3)
[semantics] recipient(theme(?1, ?2), ?3)

VP_external -> r{{ cnt.next() }}(V_dat, NP_animate_iobj, NP_inanimate_dobj) [0.10525]
[english] *(*(?1, ?2), ?3)
[semantics] theme(recipient(?1, ?2), ?3)

VP_internal -> r{{ cnt.next() }}(NP_unacc_subj, V_unacc) [1]
[english] *(?1, ?2)
[semantics] pre_theme(?2, ?1)


{## Noun phrases ##}

NP_dobj -> r{{ cnt.next() }}(NP_inanimate_dobj) [0.5]
[english] ?1
[semantics] ?1

NP_dobj -> r{{ cnt.next() }}(NP_animate_dobj) [0.5]
[english] ?1
[semantics] ?1

NP_unacc_subj  -> r{{ cnt.next() }}(Det, N_common_animate_dobj) [1]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_dobj -> r{{ cnt.next() }}(Det, N_common_animate_dobj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_dobj -> r{{ cnt.next() }}(N_prop_dobj) [0.5]
[english] ?1
[semantics] ?1


NP_animate_nsubjpass -> r{{ cnt.next() }}(Det, N_common_animate_nsubjpass) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_nsubjpass -> r{{ cnt.next() }}(N_prop_nsubjpass) [0.5]
[english] ?1
[semantics] ?1

NP_inanimate_nsubjpass -> r{{ cnt.next() }}(Det, N_common_inanimate_nsubjpass) [1.0]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

PP_iobj -> r{{ cnt.next() }} (P_iobj,NP_animate_iobj)[1]
[english] *(?1, ?2)
[semantics] pre_case(?2, ?1)

VP_passive -> r{{ cnt.next() }}(AUX,V_trans_not_omissible_pp) [0.125]
[english] *(?1, ?2)
[semantics] ?2

VP_passive -> r{{ cnt.next() }}(AUX,V_trans_not_omissible_pp, BY,NP_animate_nsubj) [0.125]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_passive -> r{{ cnt.next() }}(AUX, V_trans_omissible_pp) [0.125]
[english] *(?1, ?2)
[semantics] ?2

VP_passive -> r{{ cnt.next() }}(AUX, V_trans_omissible_pp, BY,NP_animate_nsubj) [0.125]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_passive -> r{{ cnt.next() }}(AUX,V_unacc_pp) [0.125]
[english] *(?1, ?2)
[semantics] ?2

VP_passive -> r{{ cnt.next() }}(AUX, V_unacc_pp, BY,NP_animate_nsubj) [0.125]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_passive -> r{{ cnt.next() }}(AUX, V_dat_pp, PP_iobj) [0.125]
[english] *(*(?1, ?2),?3)
[semantics] recipient(?2,?3)

VP_passive -> r{{ cnt.next() }}(AUX, V_dat_pp, PP_iobj, BY,NP_animate_nsubj) [0.125]
[english] *(*(*(?1, ?2),?3),*(?4,?5))
[semantics] agent(recipient(?2,?3),?5)

VP_passive_dat -> r{{ cnt.next() }}(AUX, V_dat_pp, NP_inanimate_dobj) [0.5]
[english] *(*(?1, ?2),?3)
[semantics] theme(?2,?3)

VP_passive_dat -> r{{ cnt.next() }}(AUX, V_dat_pp, NP_inanimate_dobj, BY,NP_animate_nsubj) [0.5]
[english] *(*(*(?1, ?2),?3),*(?4,?5))
[semantics] agent(theme(?2,?3),?5)




